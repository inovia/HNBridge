#ifndef INCLUDES
#include "../HNBridge.as"
#endif

screen 201, 640, 320
	title "HSP .NET Bridge Demo Application - 01. ZIPファイル操作"
	font "メイリオ", 17
	objmode 2
	pos 010, 010 : mes "HSP .NET Bridge Demo Application - 01. ZIPファイル操作"
	color 0xCC, 0xCC, 0xCC
	line 10, 35, 630, 35
	color
	
	font "メイリオ", 15
	pos 020, 040 : mes "HSPからHNBridgeを利用して、ZIPファイルの操作を行うデモアプリです。"
	pos 020, 060 : mes "デモアプリケーションのソースコードは、メインウインドウの「ソースコード」から"
	pos 020, 080 : mes "閲覧することができます。"

	objsize 300, 30

	folder = ""

	pos 020, 120 : input folder, 470 : inputFolder = stat
	objsize 130, 30
	pos 500, 120 : button gosub "フォルダーを選択", *demo001_selectFolder
	objsize 610, 30
	pos 020, 150 : button gosub "選択したフォルダーを圧縮", *demo001_zippingFolder : zippingButton = stat
	objenable zippingButton, 0
	
	file = ""
	
	pos 020, 210 : input file, 470 : inputFile = stat
	objsize 130, 30
	pos 500, 210 : button gosub "ファイルを選択", *demo001_selectFile
	objsize 610, 30
	pos 020, 240 : button gosub "選択したファイルを展開", *demo001_unzippingFile : unzippingButton = stat
	objenable unzippingButton, 0
	
	objsize 80, 30
	pos 540, 280 : button gosub "閉じる", *exit_201
#ifdef INCLUDES
	onexit gosub *exit
return
#else
stop
#endif

*demo001_selectFolder
	//内部クラスの場合は"+"で区切ります。
	CreateObjectFromClassName "System.Environment+SpecialFolder"
	//使わないので適当な変数に入れておきます。
	a = GetField("Desktop")
	ToInstance
	TakeDownFromCurrent "specdir"

	//mscorlib.dll のなかに無いのでFullNameで指定します。
	CreateObjectFromFullName "System.Windows.Forms", "System.Windows.Forms.FolderBrowserDialog", "4.0.0.0", "b77a5c561934e089"
	CreateInstanceWithZeroArgs
	SetProperty "Description", "フォルダーを選択してください。"
	SetProperty "RootFolder", "class://specdir"
	InvokeMethodWithZeroArgs "ShowDialog"
	folder = GetProperty("SelectedPath")
	objprm inputFolder, folder
	if(folder != "") {
		objenable zippingButton, 1
	}
return

*demo001_selectFile
	dialog "zip", 16, "ZIPファイル"
	if(stat == 0) : return
	file = refstr
	objprm inputFile, file
	if(file != "") {
		objenable unzippingButton, 1
	}
return

*demo001_zippingFolder
	CreateObjectFromFullName "System.IO.Compression.FileSystem", "System.IO.Compression.ZipFile", "4.0.0.0", "b77a5c561934e089"
	InvokeMethodWithTwoArgs "CreateFromDirectory", folder, folder + ".zip"
	dialog "圧縮完了しました。\n出力先：" + folder + ".zip"
return

*demo001_unzippingFile
	CreateObjectFromClassName "System.IO.Path"
	InvokeMethodWithOneArg "GetDirectoryName", file 
	outdir = GetLastReturnValue()
	
	CreateObjectFromFullName "System.IO.Compression.FileSystem", "System.IO.Compression.ZipFile", "4.0.0.0", "b77a5c561934e089"
	InvokeMethodWithTwoArgs "ExtractToDirectory", file, outdir
	dialog "解凍完了しました。\n出力先：" + outdir
return

*exit_201
	gsel 201, -1
return
